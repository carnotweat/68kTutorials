; ************************************
; Title
; ************************************
;
;    Title:          sysJoypad.asm
;    Author:         Ren√© Richard
;    Description:
;        
;    Target Hardware:
;        Sega Genesis / Megadrive
;    Assembler:
;        ASMX
;
; LICENSE
; 
;    This file is part of 68kTutorials.
;    68kTutorials is free software: you can redistribute it and/or modify
;    it under the terms of the GNU General Public License as published by
;    the Free Software Foundation, either version 3 of the License, or
;    (at your option) any later version.
;    68kTutorials is distributed in the hope that it will be useful,
;    but WITHOUT ANY WARRANTY; without even the implied warranty of
;    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;    GNU General Public License for more details.
;    You should have received a copy of the GNU General Public License
;    along with 68kTutorials.  If not, see <http://www.gnu.org/licenses/>.
;
; ************************************
; JOYPAD_ReadPad01:
;
; destroys:
;		A0, A1, D5, D6
; returns:
;		D6 = SACBRLDU
;		joy1State (RAM) = D6		
; ************************************
JOYPAD_ReadPad01:
	lea		IO_DATA_1, A0				; load data_1 address
	lea		v_joy1State, A1				; point to RAM placeholder for joystate
	move.b	(A0), D6					; read status j1 = 00CBRLDU
	move.b 	#$00, (A0)					; set TH low
	nop									; wait to settle
	nop									;
	move.b  (A0), D5					; read status = 00SA00DU
	rol.b	#2, D5						; SA00DU??
	andi.b	#$C0, D5					; SA000000
	or.b	D5, D6						; D6 = SACBRLDU
	not.b	D6							; invert, 1 = pressed
	move.b	#$40, (A0)					; set TH high for next pass
	move.w	D6, (A1)					; store to RAM
	rts
	
; ************************************
; sysJoy_Read3ButtonBoth
;
; destroys:
;		A0, A1, D0, D1
; ************************************					
sysJoy_Read3Button:
	lea		IO_DATA_1, A0				; load data_1 address
	lea		v_joy1State, A1				; point to RAM placeholder for joystate
	bsr.s	.read						;
	addq.w	#2, A0						;

.read
	move.b	(A0), D0					; read status of j1 = 00CBRLDU, TH already high
	move.b 	#$00, (A0)					; set TH low
	nop									; wait to settle
	nop									;
	move.b  (A0), D1					; read status  = 00SA00DU
	move.b 	#$40, (A0)					; set TH high for next pass
	rol.b	#2, D1						; SA00DU??
	andi.b	#$C0, D1					; SA000000
	or.b	D1, D0						; D0.b = SACBRLDU j1
	not.b	D0							; 1 = pressed
	move.w	(A1), D1					; get previous joypad state
	eor.w	D0, D1						; diff current with previous
	move.w	D0, (A1)+					; store current joypad state
	and.w	D0, D1
	move.w	D1, (A1)+					; store held joypad state
	rts
